* Table of Contents :toc:
- [[#header][Header]]
- [[#key-bindings][Key Bindings]]
  - [[#leader-key][Leader key]]
  - [[#hold-down-keys][Hold-down keys]]
  - [[#evil-keys][Evil keys]]
- [[#appearance][Appearance]]
- [[#settings][Settings]]
  - [[#tabs-windows-panes-etc][Tabs, Windows, Panes, etc.]]
  - [[#clipboard-settings][Clipboard Settings]]
- [[#packages][Packages]]
  - [[#org-mode][Org Mode]]
  - [[#org-modern][Org Modern]]
  - [[#projectile][Projectile]]
  - [[#perspective][Perspective]]
  - [[#neotree][Neotree]]
  - [[#centaur][Centaur]]
  - [[#magit][Magit]]
  - [[#ivy][Ivy]]
  - [[#org-trello][Org Trello]]
  - [[#leetcode][LeetCode]]
  - [[#gptel][GPTel]]
  - [[#evil][Evil]]
- [[#functions][Functions]]

* Header
#+BEGIN_SRC emacs-lisp :tangle "config.el"
;; -*- no-byte-compile: t; -*-

(add-hook 'doom-after-init-hook
          (lambda () (doom/quickload-session t)))

(defadvice! reload-with-tangle ()
  "Tangle README.org before reloading Doom Emacs."
  :before #'doom/reload
  (org-babel-tangle-file (expand-file-name "README.org" doom-private-dir)))

;; Visual line mode
(global-visual-line-mode)

;; Blink cursor mode.
(blink-cursor-mode 1)

;; Suppress warnings.
(setq warning-suppress-types
      '((obsolete)
        (bytecomp)
        (bytecomp . buffer-local-value)))
(setq enable-local-variables :all)

(after! emacs
  (find-file "~/org/tasks.org")
  (neotree))

(add-hook 'emacs-startup-hook
          (lambda ()
            (find-file "~/org/tasks.org")
            (org-mode-restart)))
#+END_SRC

* Key Bindings
** Leader key
#+BEGIN_SRC emacs-lisp :tangle "config.el"
(map! :leader
      ;; Inbox
      :desc "Babel execute source block" "c RET" #'org-babel-execute-src-block

      ;; File/directory navigation.
      :desc "Find files in homedir" "f j" (lambda () (interactive) (affe-find "~"))

      ;; Magit.
      :desc "Magit commit -m 'Update'" "g k" #'magit-commit-update

      ;; Org mode (SPC d).
      :desc "Org refile" "d f" #'org-refile
      :desc "Show DOING items" "d d" (lambda () (interactive) (org-todo-list "DOING"))
      :desc "Show NEXT items" "d n" (lambda () (interactive) (org-todo-list "NEXT"))
      :desc "Show SELECTED items" "d s" (lambda () (interactive) (org-todo-list "SELECTED"))

      :desc "Capture to inbox as DOING" "d D" (lambda () (interactive) (org-capture nil "d"))
      :desc "Capture to inbox as INBOX" "d k" (lambda () (interactive) (org-capture nil "i"))
      :desc "Capture to inbox as NEXT" "d N" (lambda () (interactive) (org-capture nil "n"))
      :desc "Capture to global inbox" "n k" (lambda () (interactive) (org-capture nil "g"))

      :desc "Open current tasks.org" "d K" (lambda () (interactive) (find-file (my/org-project-agenda-file)))
      :desc "Open global tasks.org" "n K" (lambda () (interactive) (find-file "~/org/tasks.org"))

      ;; Projectile-perspective.
      :desc "Switch project" "p p" #'projectile-persp-switch-project

      ;; Java
      :desc "Gradle test" "j j" #'gradle-test
      :desc "Gradle build" "j k" #'gradle-build

      ;; Shell interaction.
      :desc "Kill process" "m k" #'kill-process

      ;; Frequently edited files (SPC k).
      :desc "Edit config.el (README.org)" "k k" (lambda () (interactive) (find-file "~/.doom.d/README.org"))
      :desc "Edit packages.el" "k p" (lambda () (interactive) (find-file "~/.doom.d/packages.el"))
      :desc "Edit init.el" "k i" (lambda () (interactive) (find-file "~/.doom.d/init.el"))
      :desc "Edit hammerspoon config" "k h" (lambda () (interactive) (find-file "~/src/infra/hs-profiles/init.lua"))
      :desc "Edit wezterm config" "k w" (lambda () (interactive) (find-file "~/src/infra/config/wezterm/.wezterm.lua"))
      :desc "Edit tmux config" "k t" (lambda () (interactive) (find-file "~/src/infra/config/tmux/.tmux.conf"))
      :desc "Edit fish config" "k f" (lambda () (interactive) (find-file "~/src/infra/config/fish/.config/fish/config.fish"))
      :desc "Edit nvim config" "k v" (lambda () (interactive) (find-file "~/.config/nvim/lua/options.lua"))
      :desc "Edit nvim config" "k V" (lambda () (interactive) (find-file "~/.config/nvim/lua/plugins.lua"))
      )
#+END_SRC

** Hold-down keys
#+BEGIN_SRC emacs-lisp :tangle "config.el"
(map! :map global-map
      ;; Editor navigation.
      :desc "Move tab right" "s-S-<right>" #'centaur-tabs-move-current-tab-to-right
      :desc "Move tab left" "s-S-<left>" #'centaur-tabs-move-current-tab-to-left
      :desc "Change to tab right" "s-<right>" #'centaur-tabs-forward
      :desc "Change to tab left" "s-<left>" #'centaur-tabs-backward
      :desc "New tab (scratch)" "s-t" #'centaur-tabs--create-new-tab
      :desc "Close tab" "s-w" #'kill-current-buffer
      :desc "Focus pane left" "s-h" (lambda () (interactive) (move-and-maybe-maximize (lambda () (windmove-left))))
      :desc "Focus pane right" "s-l" (lambda () (interactive) (move-and-maybe-maximize (lambda () (windmove-right))))
      :desc "Focus pane up" "s-k" (lambda () (interactive) (move-and-maybe-maximize (lambda () (windmove-up))))
      :desc "Focus pane down" "s-j" (lambda () (interactive) (move-and-maybe-maximize (lambda () (windmove-down))))
      :desc "Split pane vertically" "s-d" #'split-and-balance-windows-vertically
      :desc "Split pane horizontally" "s-D" #'split-and-balance-windows-horizontally
      :desc "Previous buffer" "s-[" #'previous-buffer
      :desc "Next buffer" "s-]" #'next-buffer
      :desc "Toggle pane maximization" "s-K" #'toggle-maximize-window
      :desc "Jump to definition" "s-b" #'+lookup/definition
      :desc "Toggle compilation buffer" "s-u" #'toggle-rspec-compilation-buffer

      ;; Swiper.
      :desc "Swiper" "C-/" #'swiper

      ;; Files.
      :desc "Find file" "s-o" #'affe-find
      :desc "Grep in project" "s-i" #'affe-grep
      :desc "Create file" "s-n" #'projectile-create-new-file

      ;; Neotree.
      :desc "Toggle neotree" "s-g" #'neotree-project-dir

      ;; System clipboard.
      :desc "Paste from system clipboard" "s-v" #'paste-from-system-clipboard
      :desc "Copy to system clipboard" "s-c" #'copy-region-to-system-clipboard

      ;; Next/previous error.
      :desc "Next error" "M-]" #'next-error
      :desc "Previous error" "M-[" #'previous-error

      ;; Drag stuff.
      :desc "Drag stuff up" "M-k" #'drag-stuff-up
      :desc "Drag stuff down" "M-j" #'drag-stuff-down

      ;; Expand-region
      :desc "Expand region" "M-s-j" #'er/expand-region
      :desc "Contract region" "M-s-k" #'er/contract-region

      ;; Comments.
      :desc "Comment LOC" "s-/" (lambda () (interactive)
                                  (evilnc-comment-or-uncomment-lines 1)
                                  (forward-line 1))
      )
#+END_SRC
** Evil keys
#+BEGIN_SRC emacs-lisp :tangle "config.el"
(after! evil
  (map! :n "s-/" nil)
)
#+END_SRC
* Appearance
#+BEGIN_SRC emacs-lisp :tangle "config.el"
;; Font.
(setq doom-font (font-spec :family "Iosevka" :size 18)
      doom-variable-pitch-font (font-spec :family "Iosevka" :size 18)
      doom-big-font (font-spec :family "Iosevka" :size 26))

;; Disable line numbers.
(setq display-line-numbers-type nil)

;; Minimal UI
(menu-bar-mode -1)
(tool-bar-mode -1)
(scroll-bar-mode -1)
(load-theme 'whiteboard t)

(set-face-background 'default "#ffffff")

(set-face-attribute 'default nil :family "Iosevka")

;; Add frame borders and window dividers
(modify-all-frames-parameters
 '((right-divider-width . 40)
   (internal-border-width . 40)))
(dolist (face '(window-divider
                window-divider-first-pixel
                window-divider-last-pixel))
  (face-spec-reset-face face)
  (set-face-foreground face (face-attribute 'default :background)))
(set-face-background 'fringe (face-attribute 'default :background))
#+END_SRC
* Settings
** Tabs, Windows, Panes, etc.
#+BEGIN_SRC emacs-lisp :tangle "config.el"
(defun open-scratch-in-new-tab ()
  "Open a new tab with a *scratch* buffer."
  (interactive)
  (tab-new)
  (switch-to-buffer "*scratch*"))

(defun split-and-balance-windows-vertically ()
  (interactive)
  (split-window-right)
  (balance-windows)
  (select-window (next-window)))

(defun split-and-balance-windows-horizontally ()
  (interactive)
  (split-window-below)
  (balance-windows)
  (select-window (next-window)))

(defun close-window-or-tab ()
  (interactive)
  (if (one-window-p)
      (tab-close)
    (progn
      (delete-window)
      (balance-windows))
    ))

(defun my-list-windows ()
  "List all windows in the current tab along with their widths."
  (interactive)
  (let ((window-info '()))
    (walk-windows
     (lambda (w)
       (push (format "%s (width: %d)" (buffer-name (window-buffer w)) (window-width w)) window-info))
     nil t)
    (message "Windows in current tab: %s" (mapconcat 'identity window-info ", "))))

(defun window-is-maximized ()
  "Check if any window in the current tab has a width under 16 characters."
  (cl-some (lambda (w) (< (window-width w) 16))
           (window-list)))

(defun toggle-maximize-window ()
  "Toggle the maximization state of the current window."
  (interactive)
  (if (window-is-maximized)
      (balance-windows)    ; If the window is maximized, balance the windows.
      (maximize-window)))  ; If the window is not maximized, maximize it.

(defun move-and-maybe-maximize (move-fn)
  "Move using the lambda function MOVE-FN and maximize if the window is already maximized."
  (funcall move-fn)
  (when (window-is-maximized)
    (maximize-window)))
#+END_SRC
** Clipboard Settings
#+BEGIN_SRC emacs-lisp :tangle "config.el"
;; Disable the system clipboard.
(setq select-enable-clipboard nil)
(setq select-enable-primary nil)

;; Function to paste directly from the system clipboard
(defun paste-from-system-clipboard ()
  "Paste text from the system clipboard."
  (interactive)
  (insert (shell-command-to-string "pbpaste")))

(defun copy-region-to-system-clipboard (start end)
  "Copy the region to the system clipboard."
  (interactive "r")
  (when (display-graphic-p)
    (let ((selection-value (buffer-substring-no-properties start end)))
      (x-set-selection 'CLIPBOARD selection-value)
      (message "Region copied to system clipboard"))))
#+END_SRC

* Packages
** Org Mode
#+BEGIN_SRC emacs-lisp :tangle "config.el"
(org-babel-do-load-languages
 'org-babel-load-languages
 '((emacs-lisp . t)
   ;; Add other languages here if needed
   ))

(defun my/org-project-agenda-file ()
  "Get project's tasks.org file, if it exists."
  (expand-file-name "tasks.org" (or (projectile-project-root) "~/org/")))

(defun my/org-agenda-files ()
  (interactive)
  (list "~/org/tasks.org" (my/org-project-agenda-file)))

(defun my/org-capture-templates ()
  "Define org capture templates. Global capture, as well templates specific to current project."
  `(("g" "Global INBOX item" entry
     (file+headline "~/org/tasks.org" "Inbox")
     "** INBOX %?\n")

    ("i" "INBOX item" entry
     (file+headline ,(my/org-project-agenda-file) "Inbox")
     "** INBOX %?\n")

    ("d" "DOING item" entry
     (file+headline ,(my/org-project-agenda-file) "Inbox")
     "** DOING %?\n")

    ("n" "NEXT item" entry
     (file+headline ,(my/org-project-agenda-file) "Inbox")
     "** NEXT %?\n")

    ("s" "SELECTED item" entry
     (file+headline ,(my/org-project-agenda-file) "Inbox")
     "** SELECTED %?\n")
    ))

(after! org
  (setq
        org-capture-templates (my/org-capture-templates)
        org-agenda-files (my/org-agenda-files)
        org-todo-keywords '((sequence "INBOX" "SELECTED" "NEXT" "DOING" "POSTPONED" "BUG" "|" "DONE")))
  (map! :map org-mode-map
        "M-o" (lambda ()
                (interactive)
                (org-meta-return)
                (evil-insert 1)
                )))

(after! org-agenda
  (map! :map org-agenda-mode-map "<escape>" #'org-agenda-exit))
#+END_SRC
** Org Modern
#+BEGIN_SRC emacs-lisp :tangle "config.el"
;; Org-mode settings
(setq
 ;; Edit settings
 org-auto-align-tags nil
 org-tags-column 0
 org-catch-invisible-edits 'show-and-error
 org-special-ctrl-a/e t
 org-insert-heading-respect-content t

 ;; Org styling, hide markup etc.
 org-hide-emphasis-markers t
 org-pretty-entities t)

;; Ellipsis styling
(setq org-ellipsis "â€¦")
(set-face-attribute 'org-ellipsis nil :inherit 'default :box nil)

;; Enable org-modern
(use-package! org-modern
  :hook (org-mode . org-modern-mode)
  :config
  (setq org-modern-todo-faces '(
                                ("INBOX" :background "#1E90FF" :foreground "white")
                                ("DOING" :background "#FF8C00" :foreground "white")
                                ("NEXT" :background "#32CD32" :foreground "white")
                                ("BUG" :background "#EE4B2B" :foreground "white")
                                ("SELECTED" :background "#9B30FF" :foreground "white")
                                ))
  (global-org-modern-mode))

#+END_SRC
** Projectile
#+BEGIN_SRC emacs-lisp :tangle "config.el"
;; Projectile
(after! projectile
  (setq projectile-known-projects '(
                                    "~/.doom.d/"
                                    "~/org"
                                    "~/life"
                                    "~/src/infra/"
                                    "~/src/projects/comptus-takehome"
                                    "~/src/projects/personal-website"
                                    )
        projectile-completion-system 'default
        projectile-auto-discover nil
        projectile-cache-file (concat doom-cache-dir "projectile.cache")
        projectile-enable-caching t
        projectile-sort-order 'recentf
        projectile-require-project-root t
        projectile-switch-project-action (lambda () (find-file (expand-file-name "tasks.org" (projectile-project-root))))
        projectile-track-known-projects-automatically nil)
  (projectile-discover-projects-in-search-path)
  (add-hook 'projectile-after-switch-project-hook (lambda ()
                                                    (setq org-capture-templates (my/org-capture-templates)
                                                          org-agenda-files (my/org-agenda-files))
                                                    (message "Project org file: %s" (my/org-project-agenda-file)))))
#+END_SRC

** Perspective
#+BEGIN_SRC emacs-lisp :tangle "config.el"
(use-package! perspective
  :custom
  (persp-mode-prefix-key (kbd "C-c M-p"))
  (persp-state-default-file (expand-file-name "persp-state.el" user-emacs-directory))
  :init
  (persp-mode)
  :config
  (add-hook 'kill-emacs-hook #'persp-state-save)
  )
#+END_SRC
** Neotree
#+BEGIN_SRC emacs-lisp :tangle "config.el"
  (defun neotree-project-dir ()
    "Open NeoTree using the git root."
    (interactive)
    (let ((project-dir (projectile-project-root))
          (file-name (buffer-file-name)))
      (neotree-toggle)
      (if project-dir
          (if (neo-global--window-exists-p)
              (progn
                (neotree-dir project-dir)
                (neotree-find file-name)))
        (message "Could not find git project root."))))
#+END_SRC
** Centaur
#+BEGIN_SRC emacs-lisp :tangle "config.el"
(defun centaur-tabs-buffer-groups ()
  "Group buffers by their Projectile project."
  (if (projectile-project-p)
      (list (projectile-project-name))
    (list "Misc")))

;; Apply the custom grouping function
;; (advice-add 'centaur-tabs-buffer-groups :override #'centaur-tabs-buffer-groups)

(centaur-tabs-mode)
#+END_SRC
** Magit
#+BEGIN_SRC emacs-lisp :tangle "config.el"
;; Magit
(after! magit
  (map! :map magit-mode-map
        "<escape>" #'magit-mode-bury-buffer))

(defun magit-commit-update ()
  "Commit with message 'Update' in Magit."
  (interactive)
  (magit-commit-create `("-m" "Update")))

#+END_SRC
** Ivy
#+BEGIN_SRC emacs-lisp :tangle "config.el"
;; Ivy
(after! ivy
  (setq ivy-use-virtual-buffers t
        ivy-count-format "%d/%d "))
#+END_SRC
** Org Trello
#+BEGIN_SRC emacs-lisp :tangle "config.el"
;;;###autoload
(defun org-trello-pull-buffer (&optional from)
  "Execute the sync of the entire buffer to trello.
If FROM is non nil, execute the sync of the entire buffer from trello."
  (interactive "P")
  (org-trello--apply-deferred
   (cons 'org-trello-log-strict-checks-and-do
         (if from
             '("Request 'sync org buffer from trello board'"
               orgtrello-controller-do-sync-buffer-from-trello)
           '("Request 'sync org buffer from trello board'"
             orgtrello-controller-do-sync-buffer-from-trello)))))
#+END_SRC
** LeetCode
#+BEGIN_SRC emacs-lisp :tangle "config.el"
;; LeetCode
(setq leetcode-prefer-language "ruby")
#+END_SRC
** GPTel
#+BEGIN_SRC emacs-lisp :tangle "config.el"
(use-package auth-source
  :config
  (setq auth-sources '(macos-keychain-internet macos-keychain-generic)))

(use-package gptel
  :config
    (setq gptel-model "gpt-4o"))
#+END_SRC

** Evil
#+BEGIN_SRC emacs-lisp :tangle "config.el"
(setq evil-ex-search-case 'smart)
#+END_SRC
* Functions
#+BEGIN_SRC emacs-lisp :tangle "config.el"
(defun toggle-rspec-compilation-buffer ()
  "Toggle the visibility of the *rspec-compilation* buffer in the bottom window."
  (interactive)
  (let ((buffer (get-buffer "*rspec-compilation*")))
    (if buffer
        (if (get-buffer-window buffer)
            (delete-window (get-buffer-window buffer))
          (display-buffer buffer '((display-buffer-at-bottom)
                                   (window-height . 0.3))))
      (message "*rspec-compilation* buffer does not exist."))))

#+END_SRC
